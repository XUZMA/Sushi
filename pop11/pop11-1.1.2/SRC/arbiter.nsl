/******************** MODULE INFO  ****************************/
/*
*  File name   :  arbiter.sfl
*
*  AUTHOR      :  Yoshihiro Iida (3aepm001^keyaki.cc.u-tokai.ac.jp)
*  VERSION     :  1.0
*  DATE        :  Oct 16, 2003
*
*   Compiler    :  sfl2vl
*   Project     :  POP-11: PDP-11 compatible On Programmable chip
*   Functions   :  Interrupt arbiter of POP-11
*
*
Copyright (c) Yoshihiro Iida, Tokai University, Shimizu Lab., Japan.
(http://shimizu-lab.dt.u-tokai.ac.jp)
This software is the property of Tokai University, Shimizu Lab., Japan.

The POP-11 is free set of files; you can use it, redistribute it
and/or modify it under the following terms:

1. You are not allowed to remove or modify this copyright notice
   and License paragraphs, even if parts of the software is used.
2. The improvements and/or extentions you make SHALL be available
   for the community under THIS license, source code included.
   Improvements or extentions, including adaptions to new architectures/languages,
   SHALL be reported and transmitted to Tokai University, Shimizu Lab., Japan.
3. You must cause the modified files to carry prominent notices stating
   that you changed the files, what you did and the date of changes.
4. You may NOT distribute this set of files under another license without
   explisit permission from Tokai University, Shimizu Lab., Japan.
5. This set of files is free, and distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   You SHALL NOT use this software unless you accept to carry all
   risk and cost of defects or limitations.

*
*    ------------  CHANGE RECORD  ----------------
*    Yoshihiro Iida (3aepm001^keyaki.cc.u-tokai.ac.jp) Sep 21, 2004:
*        First free version of this software published.
*    Naohiko Shimizu (nshimizu^ip-arch.jp), Nov. 24, 2011:
*	Convert to NSL
*
*/

/* Timer */
#define DEV0_PRI 0o6
#define DEV0_VEC 0x40
/* RK Disk Controller */
#define DEV1_PRI 0o5
#define DEV1_VEC 0x90
/* Serial Receiver */
#define DEV2_PRI 0o4
#define DEV2_VEC 0x30
/* Serial Sender */
#define DEV3_PRI 0o4
#define DEV3_VEC 0x34

declare arbit_add {
  input     a[4],b[4];
  output    out;
  func_in do(a,b);
}

module arbit_add {
  wire       c0,c1,c2,c3;
  wire       g0,g1,g2,g3;
  wire       p0,p1,p2,p3;

  func do {
    g0 = a[0]&b[0];
    g1 = a[1]&b[1];
    g2 = a[2]&b[2];
    g3 = a[3]&b[3];
    p0 = a[0]^b[0];
    p1 = a[1]^b[1];
    p2 = a[2]^b[2];
    p3 = a[3]^b[3];
    c0 = g0;
    c1 = g1|(p1&g0);
    c2 = g2|(p2&g1)|(p2&p1&g0);
    c3 = g3|(p3&g2)|(p3&p2&g1)|(p3&p2&p1&g0);
    out = (p3^c2);
  }
}

declare arbiter {
  func_in   irq0;
  func_in   irq1;
  func_in   irq2;
  func_in   irq3;
  func_in   ack;
  func_out  int;
  input     cpu_pri[3];
  output    vector[8];
}

module arbiter {

  arbit_add compare;
  reg    int_req=0, int_sel[2]=0;
  reg    int_req0=0;
  reg    int_req1=0;
  reg    int_req2=0;
  reg    int_req3=0;

  {
    any {
      irq0: int_req0 := 0b1;
      irq1: int_req1 := 0b1;
      irq2: int_req2 := 0b1;
      irq3: int_req3 := 0b1;
    }
    alt {
      ack: int_req := 0b0;
      int_req0: { if(compare.do({0b1,~DEV0_PRI},{0b0,cpu_pri}).out) int_req := 0b1; int_sel := 0b00; }
      int_req1: { if(compare.do({0b1,~DEV1_PRI},{0b0,cpu_pri}).out) int_req := 0b1; int_sel := 0b01; }
      int_req2: { if(compare.do({0b1,~DEV2_PRI},{0b0,cpu_pri}).out) int_req := 0b1; int_sel := 0b10; }
      int_req3: { if(compare.do({0b1,~DEV3_PRI},{0b0,cpu_pri}).out) int_req := 0b1; int_sel := 0b11; }
    }
    if(int_req) int();
    if(ack) any {
      int_sel==0b00: { vector = DEV0_VEC; int_req0 := 0b0; }
      int_sel==0b01: { vector = DEV1_VEC; int_req1 := 0b0; }
      int_sel==0b10: { vector = DEV2_VEC; int_req2 := 0b0; }
      int_sel==0b11: { vector = DEV3_VEC; int_req3 := 0b0; }
    }
  }

}/* end of arbiter */
