/******************** MODULE INFO  ****************************/
/*
*  File name   :  idc.sfl
*
*  AUTHOR      :  Yoshihiro Iida (3aepm001@keyaki.cc.u-tokai.ac.jp)
*  VERSION     :  1.0
*  DATE        :  Oct 16, 2003
*
*   Compiler    :  sfl2vl
*   Project     :  POP-11: PDP-11 compatible On Programmable chip
*   Functions   :  Instruction decoder of POP-11
*
*
Copyright (c) Yoshihiro Iida, Tokai University, Shimizu Lab., Japan.
(http://shimizu-lab.dt.u-tokai.ac.jp)
This software is the property of Tokai University, Shimizu Lab., Japan.

The POP-11 is free set of files; you can use it, redistribute it
and/or modify it under the following terms:

1. You are not allowed to remove or modify this copyright notice
   and License paragraphs, even if parts of the software is used.
2. The improvements and/or extentions you make SHALL be available
   for the community under THIS license, source code included.
   Improvements or extentions, including adaptions to new architectures/languages,
   SHALL be reported and transmitted to Tokai University, Shimizu Lab., Japan.
3. You must cause the modified files to carry prominent notices stating
   that you changed the files, what you did and the date of changes.
4. You may NOT distribute this set of files under another license without
   explisit permission from Tokai University, Shimizu Lab., Japan.
5. This set of files is free, and distributed in the hope that it will be
   useful, but WITHOUT ANY WARRANTY; without even the implied warranty
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
   You SHALL NOT use this software unless you accept to carry all
   risk and cost of defects or limitations.

*
*    ------------  CHANGE RECORD  ----------------
*    Yoshihiro Iida (3aepm001@keyaki.cc.u-tokai.ac.jp) Sep 21, 2004:
*        First free version of this software published.
*    Naohiko Shimizu (nshimizu@ip-arch.jp), Nov. 24, 2011:
*	Convert to NSL
*
*/

declare idc {
  input     idc_opc[16];
  func_out  sop,   dop,   rsd,  nof,  bra,   cco, unused;
  func_out  dhalt, diwait,   drti,  dbpt,  diot, dreset,  drtt,  djmp,  drts,
            dnop,  dswab,   djsr,  dclr,  dcom,   dinc,  ddec,  dneg,  dadc, dsbc,
            dtst,   dror,   drol,  dasr,  dasl,  dmark, dmfpi, dmtpi,  dsxt,
            dmov,   dcmp,   dbit,  dbic,  dbis,   dadd,  dmul,  ddiv,  dash, dashc,
            dexor,  dfadd,  dfsub, dfmul, dfdiv,   dsob,  demt, dtrap, dmfpd,
            dmtpd,  dsub, dfloat,  dspl;
  func_in do(idc_opc);
}

module idc {

  func do any {
    (idc_opc[15:00] == {0b0,0o00000}): { nof(); dhalt(); }
    (idc_opc[15:00] == {0b0,0o00001}): { nof(); diwait(); }
    (idc_opc[15:00] == {0b0,0o00002}): { nof(); drti(); }
    (idc_opc[15:00] == {0b0,0o00003}): { nof(); dbpt(); }
    (idc_opc[15:00] == {0b0,0o00004}): { nof(); diot(); }
    (idc_opc[15:00] == {0b0,0o00005}): { nof(); dreset(); }
    (idc_opc[15:00] == {0b0,0o00006}): { nof(); drtt(); }
    (idc_opc[15:00] == {0b0,0o00007}): { nof(); unused(); }

    (idc_opc[15:06] == {0b0,0o001  }): { dop(); djmp(); }
    (idc_opc[15:03] == {0b0,0o0020 }): { dop(); drts(); }
    (idc_opc[15:03] == {0b0,0o0021 }): { nof(); unused(); }
    (idc_opc[15:03] == {0b0,0o0022 }): { nof(); unused(); }
    (idc_opc[15:03] == {0b0,0o0023 }): { nof(); dspl(); }

    (idc_opc[15:04] == {0b0,0o002,0b10})&(~|idc_opc[3:0]): { nof(); dnop(); }
    (idc_opc[15:04] == {0b0,0o002,0b10})&( |idc_opc[3:0]): { cco(); }
    (idc_opc[15:04] == {0b0,0o002,0b11}):               { cco(); }

    (idc_opc[15:06] == {0b0,0o003}): { dop(); dswab(); }

    (idc_opc[15:11] == {0b0,0o0,0b0})&(|idc_opc[10:8]): { bra(); }
    (idc_opc[15:11] == {0b1,0o0,0b0}):               { bra(); }

    (idc_opc[15:09] == {0b0,0o04}): { dop(); djsr(); }

    (idc_opc[14:06] == 0o050): { dop(); dclr(); }
    (idc_opc[14:06] == 0o051): { dop(); dcom(); }
    (idc_opc[14:06] == 0o052): { dop(); dinc(); }
    (idc_opc[14:06] == 0o053): { dop(); ddec(); }
    (idc_opc[14:06] == 0o054): { dop(); dneg(); }
    (idc_opc[14:06] == 0o055): { dop(); dadc(); }
    (idc_opc[14:06] == 0o056): { dop(); dsbc(); }
    (idc_opc[14:06] == 0o057): { dop(); dtst(); }
    (idc_opc[14:06] == 0o060): { dop(); dror(); }
    (idc_opc[14:06] == 0o061): { dop(); drol(); }
    (idc_opc[14:06] == 0o062): { dop(); dasr(); }
    (idc_opc[14:06] == 0o063): { dop(); dasl(); }

    (idc_opc[15:06] == {0b0,0o064}): { nof(); dmark(); }
    (idc_opc[15:06] == {0b0,0o065}): { dop(); dmfpi(); }
    (idc_opc[15:06] == {0b0,0o066}): { dop(); dmtpi(); }
    (idc_opc[15:06] == {0b0,0o067}): { dop(); dsxt(); }

    (idc_opc[15:09] == {0b0,0o07}): { nof(); unused(); }

    (idc_opc[14:12] == 0o1): { sop(); dmov(); }
    (idc_opc[14:12] == 0o2): { sop(); dcmp(); }
    (idc_opc[14:12] == 0o3): { sop(); dbit(); }
    (idc_opc[14:12] == 0o4): { sop(); dbic(); }
    (idc_opc[14:12] == 0o5): { sop(); dbis(); }
    (idc_opc[15:12] == {0b0,0o6}): { sop(); dadd(); }
    (idc_opc[15:12] == {0b1,0o6}): { sop(); dsub(); }

    (idc_opc[15:09] == {0b0,0o70}): { rsd(); dmul(); }
    (idc_opc[15:09] == {0b0,0o71}): { rsd(); ddiv(); }
    (idc_opc[15:09] == {0b0,0o72}): { rsd(); dash(); }
    (idc_opc[15:09] == {0b0,0o73}): { rsd(); dashc(); }
    (idc_opc[15:09] == {0b0,0o74}): { rsd(); dexor(); }

    (idc_opc[15:03] == {0b0,0o7500}): { rsd(); dfadd(); }
    (idc_opc[15:03] == {0b0,0o7501}): { rsd(); dfsub(); }
    (idc_opc[15:03] == {0b0,0o7502}): { rsd(); dfmul(); }
    (idc_opc[15:03] == {0b0,0o7503}): { rsd(); dfdiv(); }

    (idc_opc[15:05] == {0b0,0o750,0b1}):           { nof(); unused(); }
    (idc_opc[15:09] == {0b0,0o75})&(|idc_opc[8:6]): { nof(); unused(); }
    (idc_opc[15:09] == {0b0,0o76}):              { nof(); unused(); }

    (idc_opc[15:09] == {0b0,0o77}):              { nof(); dsob(); }

    (idc_opc[15:09] == {0b1,0o04})&(~idc_opc[8]): { nof(); demt(); }
    (idc_opc[15:09] == {0b1,0o04})&( idc_opc[8]): { nof(); dtrap(); }

    (idc_opc[15:06] == {0b1,0o064}): { nof(); unused(); }

    (idc_opc[15:06] == {0b1,0o065}): { nof(); dmfpd(); }
    (idc_opc[15:06] == {0b1,0o066}): { nof(); dmtpd(); }

    (idc_opc[15:06] == {0b1,0o067}): { nof(); unused(); }
    (idc_opc[15:09] == {0b1,0o07 }): { nof(); unused(); }

    (idc_opc[15:12] == {0b1,0o7 }): { nof(); dfloat(); }
  }
}

